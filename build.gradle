//file:noinspection GroovyAssignabilityCheck
import static org.gradle.api.file.DuplicatesStrategy.INCLUDE

plugins {
    id 'net.minecraftforge.gradle' version '6.+'
    id 'org.spongepowered.mixin' version '0.+'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
}

version = "$minecraft_version-$mod_version"
group = maven_group

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of 17
    }
}

minecraft {
    mappings channel: 'parchment', version: "$parchment_version-$minecraft_version"
    accessTransformer = file 'src/main/resources/META-INF/accesstransformer.cfg'
    def mixinArgs = [ '-Dmixin.debug.export=true', '-Dmixin.hotSwap=true', '-Dmixin.checks.interfaces=true',
                 '-mixin.config="reputation.mixin.json"' ]
    runs {
        configureEach {
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            args mixinArgs
            mods {
                reputation {
                    //noinspection GroovyAssignabilityCheck
                    source sourceSets.main
                }
            }
        }
        client {
            workingDirectory file('run_client')
        }
        server {
            workingDirectory file('run_server')
        }
        data {
            workingDirectory file('run_data')
            args '--mod', 'reputation', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }
    }
}

repositories {
    maven {
        name = 'CurseMaven'
        url = uri 'https://curse.cleanroommc.com'
        content {
            includeGroup 'curse.maven'
        }
    }
}

dependencies {
    //noinspection VulnerableLibrariesLocal
    minecraft "net.minecraftforge:forge:$minecraft_version-$forge_version"

    //Mixin
    annotationProcessor "org.spongepowered:mixin:$mixin_version:processor"

    //Locally Implemented Mods
    implementation fg.deobf("curse.maven:quest-giver-618562:$questgiver_version")

    //Implemented Mods
    implementation fg.deobf("curse.maven:guard-villagers-360203:$guards_version")
    implementation fg.deobf("curse.maven:morevillagers-484954:$morevillagers_version")
    implementation fg.deobf("curse.maven:the-impossible-library-661115:$til_version")

    //Runtime Mods
    runtimeOnly fg.deobf("curse.maven:afptweaks-638098:$afptweaks_version")
    runtimeOnly fg.deobf("curse.maven:atlas-lib-463826:$atlaslib_version")
    runtimeOnly fg.deobf("curse.maven:configured-457570:$configured_version")
    runtimeOnly fg.deobf("curse.maven:epicfigt-405076:$epicfight_version")
    runtimeOnly fg.deobf("curse.maven:follow-me-463841:$followme_version")
    runtimeOnly fg.deobf("curse.maven:jei-238222:$jei_version")
    runtimeOnly fg.deobf("curse.maven:libx-412525:$libx_version")
}

mixin {
    add sourceSets.main, 'reputation.refmap.json'
}

processResources {
    duplicatesStrategy = INCLUDE
    from(sourceSets.main.resources.srcDirs) {
        include 'META-INF/mods.toml'
        expand 'version': version
    }
}

jar {
    manifest {
        attributes([
                "Implementation-Title": name,
                "MixinConfigs": "reputation.mixin.json",
                "Specification-Version": version,
                "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
                "TweakOrder": 0
        ])
    }
}

tasks.jar.finalizedBy 'reobfJar'